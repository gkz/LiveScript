I = (x) -> x
M = (x) -> x x

eq I, M I


# Bare calls.
eq I (), I( )


# The empty function should not cause a syntax error.
->
(0 while 0).pop()


# Multiple nested function declarations mixed with implicit calls should not
# cause a syntax error.
(one) -> (two) -> three four, (five) -> six seven, eight, (nine) ->


obj = {
  bound   : -> do ~> this
  unbound : -> do -> this
  nested  : -> do ~> do ~> do ~> this
}
eq obj, obj.bound()
ok obj is not obj.unbound()
eq obj, obj.nested()


# Multi-blocks with optional parens.
result = I( ->
  I ->
    "Wrapped"
)
eq result()(), 'Wrapped'


eq 'lo', ("hello".slice) 3


# Nested single-line functions.
func = (x) -> (x) -> (x) -> x
eq func(1)(2)(3), 3


# Trailing comma/semicolon in a function parameter.
eq 1, ((f,) -> f()) (,) -> 1
eq 2, ((f;) -> f()) (;) -> 2


# Implicit calls in functions in parens.
eq 10, ((val) ->
  [].push val
  val
)(10)


# Passing multiple multiline functions without paren-wrapping.
sum = (one, two) -> one() + two()
eq 6, sum(->
  1 + 2
, ->
  2 + 1
)
eq 20, sum ->
  7 + 9
, ->
  /* spacer */
  1 + 3


# Implicit calls against trailing conditional branches.
eq 2, if 0 then 1 else 2
eq 6, switch 3 case 4 then 5 default 6


# Implicit calls using leading commas.
eq 1
,  1
eq \
  2
, 2

a = Array 0
        , do ->
          1
        , do -> 2; 2
        , 3
eq a+'' '0,1,2,3'


# Assignment to an inner variable that shares a name with
# an `Object.prototype` member should not leak.
(-> constructor = 'word')()
ok constructor is not 'word'


# Implicit call including an implicit object and a trailing function.
meth = (arg, obj, func) -> String [obj.a, arg, func()]
eq '13,apple,orange', meth 'apple', b: 1, a: 13, ->
  'orange'


# Ensure that empty functions don't return mistaken values.
o = func: (@param, ...@rest) ->

eq void , o.func(101, 102, 103, 104)
eq 101  , o.param
eq '102,103,104', '' + o.rest


(->
  this it, it
  @    it, it
).call ok, '`this` should be able to call implicitly'


# `new` should apply to the first function of a call chain,
args = [1 2]
eq 3, new (-> fun: (x, y) -> x + y)().fun ...args
eq 3, new (->      (x, y) -> x + y)()     ...args
eq 4 (new new Function 'this.p = 4')p
eq 5  new new Function('this.q = 5')()q

# but not to helper functions.
eq 1, new [...[-> [1]]].0().0             # __slice
eq 'object', typeof new {f: Number}.~f()  # __bind


# Chained blocks, with proper indentation levels:
counter =
  results: []
  tick: (func) ->
    @results.push func()
    this

counter
.tick ->
   3
.tick ->
  2
.tick ->
 1

eq counter.results.join(' '), '3 2 1'


# Newline-supressed call chains with nested functions.
obj  = call: -> this
func = ->
  obj
  .call ->
    one two
  .call ->
      three four
  101

eq func(), 101


ok new Date().constructor is Date
, '`new` should not add extra parens'

ok new (Object C: Number).C instanceof Number
, '`new` should not unwrap parenthesized operand'


# `new` against bare function prevents implicit return.
o = new ->
  @0 = 1
  2
eq o.0, 1


# Implicit calls against non-spaced unary plus/minus.
eq +5, +5
eq -5, -5


# Implicit calls against precrements.
n = 0
eq ++n, 1
eq --n, 0


eq ok, do ->
  ok
  /* Should `return` implicitly   */
  /* even with trailing comments. */


# Returns with multiple branches.
func = ->
  if it
    return n for n of [1, 2]
  else
    0
eq func(0), 0
eq func(1), 1


# Don't gather results from a loop that _jumps_ out of a closure.
findIt = (items) -> return item if item is 'bacon' for item of items
eq 'bacon', findIt [1, 2, 3, 'bacon', 4, 5]
eq void   , findIt []


# When a closure wrapper is generated for expression conversion, make sure
# that references to "this" within the wrapper are safely converted as well.
obj = method: -> (switch case 1 then this)
eq obj.method(), obj


eq 3, do -> (1; 2; 3)
eq 3, do -> return (1; 2; 3)


compileThrows 'inconvertible statement' 1 'b = break'
compileThrows 'inconvertible statement' 2 '''
  r =
    return
'''
compileThrows 'inconvertible statement' 3 '''
  r = if 1
    2 +
    return
'''


eq '(function(){})(function(){});', Coco.compile '(->return)(->void)', {+bare}


# `@it` isn't `it`
eq '''
(function(){
  return this.it;
});
''', Coco.compile '-> @it', {+bare}


# Simple functions require no parens when comma-listed.
funs = [->, -> 1, -> it, -> this, null]
eq 1, +funs.3.call funs.2 funs.1()

# [#81](https://github.com/satyr/coco/issues/81)
ok I(->), 'be careful when specialcasing `-> X ,`'


eq 0, (new do -> Array).length


x = y = 10; x1 = y1 = 20
area = (x, y, x1, y1) ->
  (x - x1) * (x - y1)

eq area(x, y, x1, y1), 100
eq(area(
  x
  y
  x1, y1
), 100)


sumOfArgs = ->
  sum = 0
  sum += val for val of arguments
  sum

eq 15, sumOfArgs(1, 2, 3, 4, 5)


((@arg) ->).call context = {}, 1
eq 1, context.arg

((...splat, @arg) ->).call context, 1, 2, 3
eq 3, context.arg

((...@arg) ->).call context, 1, 2, 3
eq '1,2,3', '' + context.arg


eq 1, new (class then (@do) -> eq @do, $do)(1).do


# Parameter destructuring
((...[{a: [b], c}]) ->
  eq b, 123
  eq c, 456
) {a: [123], c: 456}


# Parameter default values
obj = f: (q = 123, @p = 456) -> q
eq obj.f(), 123
eq obj.p  , 456

withSplats = (a = 2, ...b, c = 3, d ? 5) -> a * (b.length + 1) * c * d
eq 30, withSplats()
eq 15, withSplats 1
eq  5, withSplats 1, 1
eq  1, withSplats 1, 1, 1
eq  2, withSplats 1, 1, 1, 1

f = (a || 2, b && 5) -> a + b
eq 7, f 0, 1
eq 1, f 1, 0
eq 6, f 1, 1
eq 2, f 0, 0

do (a ? I(1)) -> eq a, 1

eq 1, do []= (a || 0 || 1) -> a


eq arguments,
  switch case 1
    eq arguments, (arguments for i to 0)0
    arguments


ok (@arguments, @eval) ->


compileThrows 'duplicate parameter "a"' 1 '(a, a) ->'


# Fun with radical parameters.
obj = {}
set = (name, obj[name]) ->
set \key \value
eq obj.key, \value


# Call takes precedence over function parameter.
eq 0, I(-> it()) -> 0
eq void Function() ->


### Invalid call detection
compileThrows 'invalid callee'      1 '[]()'
compileThrows 'invalid constructor' 1 'new 42'


### `new` block
o = new
  @1 = \_
  @length = 3
eq '0_0' o * \0


### Backcalls
g = (...a, f) -> f ...a
h = (f, ...a) -> f a

eq ok, do
  (a, b) <- g \a, \b
  eq b, \b
  ...d <- h ..., a
  eq d.0.0, \a
  ok

new
  me = this
  f <~ M
  eq me, this
  eq \function typeof f

eq 3 do
  do
    <- I 1
    2
  3

eq 6 (a <- g 6; a)


### `function`
new
  function undef1 then
  function undef2
    void
  eq void undef1()
  eq void undef2()

  ~function bound then this
  eq this, bound.call \this

  f = act: function (n)
    if n < 2 then 1 else n * act n-1
  eq 120 f.act 5

  ok function ok then throw
  eq void, do f = function then

  function double(a) then a * 2
  function triple a  then a * 3
  eq 4, double 2
  eq 9, triple 3

compileThrows 'redeclaration of function "f"' 2 '''
  f = 0
  function f then
'''
compileThrows 'redeclaration of function "f"' 2 '''
  function f
    f = 1
'''
compileThrows 'redeclaration of function "f"' 2 '''
  function f then
  f = 2
'''
compileThrows 'redeclaration of function "f"' 1 '''
  function f f then
'''
compileThrows 'increment of function "f"' 2 '''
  function f then
  ++f
'''
compileThrows 'misplaced function declaration' 2 'if 1\n function F then'


### `let`
new
  x = y = 1; @z = 2
  let x = 0, y, @z
    eq x, 0
    eq y, 1
    eq z, 2
    x = y = 3
  eq x, 1
  eq y, 1

eq \chainable,
  let
    \chain
  .concat \able

eq '''
(function(){
  this;
}.call(this));
''' Coco.compile 'let() then this # top-level and should not return' {+bare}

ok let [it] = [ok]
  it is ok


### `with`
with o = {}
  @a = 1
  @b = 2
eq o.a + o.b, 3
eq ok, with ok then this
ok with ok
  this is ok


### `@@`
let 0
  eq @@ , arguments
  eq @@0, 0
  eq @@1, void


### thisplat
f = (x, y) -> [this, x, y]
with 0 then let x = 1, y = 2
  eq '0,1,2' ''+ f ...
  eq ','     ''+ f(...[])slice 1


### do-`not`-return
eq void do !-> true
eq void do !~> true
eq void do
  <-! M
  <~! M
  !function C then C
  ok new C instanceof C
  true
eq false !!->


### explicit naming
let
  do a = :b -> eq a, b
  do c = :d!-> eq c, d
  do e = !:f-> eq e, f
let
  a <-:b  M
  c <-:d! M
  e <-!:f M
  eq a, b
  eq c, d
  eq e, f
